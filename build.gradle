buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath JavaVersion.current() == JavaVersion.VERSION_1_8 ? "net.ltgt.gradle:gradle-errorprone-plugin:0.0.16" : "net.ltgt.gradle:gradle-errorprone-javacplugin-plugin:0.3"
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.13.0" apply false
}

wrapper {
    gradleVersion = '4.8.1'
    distributionType = Wrapper.DistributionType.ALL
}

apply from: "gradle/compile.gradle"

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.diffplug.gradle.spotless'
    apply plugin: JavaVersion.current() == JavaVersion.VERSION_1_8 ? "net.ltgt.errorprone" : "net.ltgt.errorprone-javacplugin"

    group = 'org.zaproxy'

    version '1.7.0-SNAPSHOT'
    ext.versionBC = '1.6.0'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    spotless {
        java {
            licenseHeaderFile "$rootDir/gradle/spotless/license.java"

            googleJavaFormat().style('AOSP')
        }
    }

    dependencies {
        errorprone 'com.google.errorprone:error_prone_core:2.3.1'
    }
}
